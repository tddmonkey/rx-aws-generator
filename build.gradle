apply plugin: 'java'
apply plugin: 'idea'

repositories {
	mavenLocal()
	jcenter()
}

def awsVersion = "1.11.119-SNAPSHOT"

dependencies {
	compile(
		'io.reactivex:rxjava:1.1.0',
		'com.squareup:javapoet:1.5.1',
		"com.amazonaws:aws-java-sdk-sqs:$awsVersion",
		"com.amazonaws:aws-java-sdk-dynamodb:$awsVersion",
		"com.amazonaws:aws-java-sdk-sns:$awsVersion",
	)

	testCompile(
	)
}

sourceSets {
	generated {
		java {
			srcDir 'src/generated/java'
			compileClasspath += main.output
			runtimeClasspath += main.output
		}
	}
}

idea {
	module {
		sourceDirs += file('src/generated/java')
		generatedSourceDirs += file('src/generated/java')
	}
}

task generateSqsSdk(type: JavaExec) {
	main = 'generator.common.Start'
	classpath = sourceSets.main.runtimeClasspath
	args "RXSQS", file("src/generated/java/")
}

task generateDynamoSdk(type: JavaExec) {
	main = 'generator.common.Start'
	classpath = sourceSets.main.runtimeClasspath
	args "RXDYNAMO", file("src/generated/java/")
}

task generateSnsSdk(type: JavaExec) {
	main = 'generator.common.Start'
	classpath = sourceSets.main.runtimeClasspath
	args "RXSNS", file("src/generated/java/")
}

task generateAllSdks() { }

generateAllSdks.dependsOn generateSqsSdk
generateAllSdks.dependsOn generateDynamoSdk
generateAllSdks.dependsOn generateSnsSdk

generateSqsSdk.dependsOn classes
generateDynamoSdk.dependsOn classes
generateSnsSdk.dependsOn classes
